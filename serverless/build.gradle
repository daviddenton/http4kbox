buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'application'
mainClassName = "http4kbox.LambdaLoader"
apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    baseName = "serverless"
    classifier = null
    version = null
    mergeServiceFiles()
}

dependencies {
    compile project(":app")
    compile "org.http4k:http4k-serverless-lambda:$http4k_version"
    testCompile "io.github.s4nchez:deployer:0.1-SNAPSHOT" // <- used by the deployment tasks
}

task deployLambda(type: JavaExec) {
    main = 'io.github.s4nchez.DeployLambdaKt'
    environment "DEPLOYMENTSPEC_MODULE", project.name
    environment "DEPLOYMENTSPEC_LAMBDAFILE", "build/libs/serverless.jar"
    environment "DEPLOYMENTSPEC_LAMBDATIMEOUT", "20"
    environment "DEPLOYMENTSPEC_HANDLERFUNCTION", "org.http4k.serverless.lambda.LambdaFunction::handle"
    environment "HTTP4K_BOOTSTRAP_CLASS", "http4kbox.Http4kboxLambda"
    environment "CREDENTIALS", "http4kbox:http4kbox"

    classpath += sourceSets.main.resources
    classpath += sourceSets.test.compileClasspath
}

task deployApi(type: JavaExec) {
    main = 'io.github.s4nchez.DeployApiKt'
    environment "DEPLOYMENTSPEC_MODULE", project.name

    classpath += sourceSets.main.resources
    classpath += sourceSets.test.compileClasspath
}

deployLambda.dependsOn build
